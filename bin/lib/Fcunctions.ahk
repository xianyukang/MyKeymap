#Include TypoTipWindow.ahk
#Include TempFocusGui.ahk

/**
 * æ‰˜ç›˜èœå•è¢«ç‚¹å‡»
 * @param ItemName 
 * @param ItemPos 
 * @param MyMenu 
 */
TrayMenuHandler(ItemName, ItemPos, MyMenu) {
  switch ItemName {
    case "é€€å‡º":
      ExitApp
    case "æš‚åœ":
      ToggleSuspend()
    case "é‡å¯ç¨‹åº":
      ReloadPropram()
    case "æ‰“å¼€è®¾ç½®":
      OpenSettings()
    case "å¸®åŠ©æ–‡æ¡£":
      Run("https://xianyukang.com/MyKeymap.html")
    case "æŸ¥çœ‹çª—å£æ ‡è¯†ç¬¦":
      run("MyKeymap.exe bin\WindowSpy.ahk")

  }
}

/**
 * é€€å‡ºç¨‹åº
 * @param ExitReason é€€å‡ºåŸå› 
 * @param ExitCode ä¼ é€’ç»™ Exit æˆ– ExitApp çš„é€€å‡ºä»£ç .
 */
MyExit(ExitReason, ExitCode) {
  if (capsAbbrWindowPid)
    ProcessClose(capsAbbrWindowPid)
}

/**
 * æš‚åœ
 */
ToggleSuspend() {
  Suspend(!A_IsSuspended)
  if (A_IsSuspended) {
    TraySetIcon("./bin/icons/logo2.ico", , 1)
    A_TrayMenu.Check("æš‚åœ")
    Tip("  æš‚åœ MyKeymap  ", -500)
  } else {
    TraySetIcon("./bin/icons/logo.ico", , 0)
    A_TrayMenu.UnCheck("æš‚åœ")
    Tip("  æ¢å¤ MyKeymap  ", -500)
  }
}

/**
 * æ‰“å¼€è®¾ç½®
 */
OpenSettings() {
  if (!WinExist("\bin\settings.exe"))
    Run("./bin/settings.exe ./bin")

  try {
    WinActivate("MyKeymap Settings")
  } catch Error as e {
    Run("http://127.0.0.1:12333")
  }
}

/**
 * é‡å¯ç¨‹åº
 */
ReloadPropram() {
  Tip("Reload")
  Run("MyKeymap.exe")
}

/**
 * å…³é—­æ‰€æœ‰æ¨¡å¼
 * @param Thrown æŠ›å‡ºçš„å€¼, é€šå¸¸ä¸º Error å¯¹è±¡
 * @param Mode é”™è¯¯çš„æ¨¡å¼: Return, Exit æˆ– ExitApp
 */
CloseAllMode(Thrown?, Mode?) {
  global customHotKey := true

  global capslockMode := false
  global jMode := false
  global semicolonMode := false
  global threeMode := false
  global nincMode := false
  global commaMode := false
  global dotModel := false
  global additionalMode1 := false
  global additionalMode2 := false
  global spaceMode := false
  global tabMode := false
  global rButtonMode := false
  global lButtonMode := false

  global capsFMode := false
  global capsSpaceMode := false
  global jKModel := false

  global mouseMode := false
  global TaskSwitchMode := false

  global modeState
  modeState.locked := false
}

/**
 * è‡ªåŠ¨å…³é—­çš„æç¤ºçª—å£ 
 * @param message è¦æç¤ºçš„æ–‡æœ¬
 * @param {number} time è¶…æ—¶åå…³é—­
 */
Tip(message, time := -1500) {
  ToolTip(message)
  SetTimer(() => ToolTip(), time)
}

/**
 * è·å–é¼ æ ‡ç§»åŠ¨æ—¶çš„æç¤ºçª—å£
 */
GetMouseMovePromptWindow() {
  return TypoTipWindow("ğŸ–±", 16, 4, 0)
}

/**
 * ç§»åŠ¨é¼ æ ‡
 * @param key æŒ‰ä¸‹çš„å€¼
 * @param directionX å‘å·¦-1 å‘å³1
 * @param directionY å‘ä¸Š-1 å‘ä¸‹1
 * @param moveSingle é¦–æ¬¡ç§»åŠ¨çš„æ­¥é•¿
 * @param moveRepeat ç§»åŠ¨çš„æ­¥é•¿
 * @param {number} showTip æ˜¯å¦æç¤ºå½“å‰ä¸ºé¼ æ ‡æ¨¡å¼
 */
MoveMouse(key, directionX, directionY, moveSingle, moveRepeat, showTip := false) {
  oneX := directionX * moveSingle
  oneY := directionY * moveSingle
  repeatX := directionX * moveRepeat
  repeatY := directionY * moveRepeat
  MouseMove(oneX, oneY, 0, "R")

  if showTip
    showTip.Show(, 19, 17)

  f() {
    if showTip
      showTip.Show(, 19, 17)
    MouseMove(repeatX, repeatY, 0, "R")
  }

  WhileKeyWait(key, moveDelay1, moveDelay2, f)
}

/**
 * å½“æŒ‰é”®ç­‰å¾…æ—¶æ‰§è¡Œçš„æ“ä½œ
 * @param key æŒ‰ä¸‹çš„å€¼
 * @param delay1 é¦–æ¬¡ç­‰å¾…çš„æ—¶é—´
 * @param delay2 ç­‰å¾…çš„æ—¶é—´
 * @param func å½“è¶…è¿‡ç­‰å¾…æ—¶é—´æ‰§è¡Œçš„æ–¹æ³•
 */
WhileKeyWait(key, delay1, delay2, func) {
  i := KeyWait(key, delay1)
  while (!i) {
    func()

    i := KeyWait(key, delay2)
  }
}

/**
 * é€€å‡ºé¼ æ ‡ç§»åŠ¨æ¨¡å¼
 */
ExitMouseMode() {
  global mouseMode := false

  Send("{Blind}{LButton up}")

  if (IsSet(mousemovePrompt))
    mousemovePrompt.show
}

/**
 * æ¨¡æ‹Ÿé¼ æ ‡ç‚¹å‡»åæ¨å‡º
 * @param key æ¨¡æ‹Ÿé¼ æ ‡çš„é”®
 */
MouseClickAndExit(key) {
  Send("{blind}" key)
  if (needExitMouseMode)
    ExitMouseMode()
}

/**
 * æ»šè½®æ»‘åŠ¨ä¸€æ¬¡
 * @param direction æ–¹å‘
 *   1:ä¸Š
 *   2:ä¸‹
 *   3:å·¦
 *   4:å³
 * @param {number} scrollCount æ»‘åŠ¨æ¬¡æ•°
 */
ScrollWheelOnce(direction, scrollCount := 1) {
  switch (direction) {
    case 1: MouseClick("WheelUp", , , scrollCount)
    case 2: MouseClick("WheelDown", , , scrollCount)
    case 3: MouseClick("WheelLeft", , , scrollCount)
    case 4: MouseClick("WheelRight", , , scrollCount)
  }
}

/**
 * å†»ç»“éæŒ‡å®šçš„æ¨¡å¼
 * @param modeName æ¨¡å¼åç§°
 */
FreezeOtherMode(modeName) {
  global activatedModes, customHotKey, altTabIsOpen, modeState
  customHotKey := true
  altTabIsOpen := false

  ; æ¯”å¦‚é”å®šäº† 3, ä½†åŒæ—¶æƒ³ç”¨ 9 æ¨¡å¼çš„çƒ­é”®, éœ€è¦ä¸´æ—¶å–æ¶ˆé”å®š
  if (modeState.locked) {
    %modeState.currentRef% := false
  }

  for index, value in activatedModes {
    if (value != modeName) {
      Hotkey(value, "Off")
    }
  }
}

/**
 * é‡ç½®å½“å‰è¿è¡Œçš„çƒ­é”®
 * @param modeName æ¨¡å¼çš„åç§°
 * @param modeRef æ¨¡å¼å˜é‡çš„å¼•ç”¨
 * @returns {void} 
 */
ResetCurrentMode(modeName, &modeRef) {
  global modeState
  if (modeState.locked)
    return

  modeState.currentName := modeName
  ; æŒ‡å®šå…¶å¼•ç”¨ï¼Œä¸ç„¶åé¢æ— æ³•æ”¹æ¨¡å¼çš„çŠ¶æ€
  modeState.currentRef := &modeRef
}

/**
 * è§£å†»éæŒ‡å®šçš„æ¨¡å¼
 * @param modeName æ¨¡å¼åç§°
 */
UnfreezeMode(modeName) {
  global activatedModes, customHotKey, altTabIsOpen, modeState
  customHotKey := false

  if (altTabIsOpen) {
    altTabIsOpen := false
    Send("{Enter}")
  }

  ; å¯åŠ¨è¢«é”å®šçš„æ¨¡å¼
  if (modeState.locked) {
    %modeState.currentRef% := true
  }

  for index, value in activatedModes {
    if (value != modeName)
      Hotkey(value, "On")
  }

}

/**
 * å¯åŠ¨æŒ‡å®šMode
 * @param modeRef æ¨¡å¼å˜é‡çš„å¼•ç”¨
 * @param modeName æ¨¡å¼çš„åç§°
 * @param mil è¶…æ—¶æ—¶é—´
 * @param func éè¶…æ—¶æ‰§è¡Œçš„æ“ä½œ
 * @param {number} needFreezeOtherMode æ˜¯å¦éœ€è¦è§£å†»å…¶ä»–æ¨¡å¼ï¼ŒäºŒçº§æ¨¡å¼ä¸éœ€è¦è§£å†»ï¼Œæ¯”å¦‚CaspFæ¨¡å¼ã€CapsSpaceæ¨¡å¼ç­‰ã€‚
 */
EnableMode(&modeRef, modeName, mil?, func?, needFreezeOtherMode := true) {
  statrtTick := A_TickCount
  thisHotKey := A_ThisHotkey
  modeRef := true
  ; Caps Fã€Caps ç©ºæ ¼ä¹‹ç±»çš„äºŒçº§æ¨¡å¼æ˜¯ä¸ç”¨è§¦å‘å†»ç»“çš„
  if (needFreezeOtherMode) {
    FreezeOtherMode(ThisHotkey)
    ResetCurrentMode(modeName, &modeRef)
  }
  KeyWait(thisHotKey)
  modeRef := false

  if (IsSet(mil))
    if ((A_PriorKey != "" && A_PriorKey = thisHotkey) && A_TickCount - statrtTick < mil)
      if (IsSet(func))
        func()

  ; å› ä¸ºæ²¡æœ‰è§¦å‘å†»ç»“æ‰€ä»¥ä¸éœ€è¦è§£å†»
  if (needFreezeOtherMode) {
    UnfreezeMode(ThisHotkey)
  }
}

/**
 * è·å–å½“å‰ç¨‹åºåç§°
 * è‡ªå¸¦çš„WinGetProcessNameæ— æ³•è·å–åˆ°uwpåº”ç”¨çš„åç§°
 * æ¥æºï¼šhttps://www.autohotkey.com/boards/viewtopic.php?style=7&t=112906
 * @returns {string} 
 */
GetProcessName() {
  fn := (winTitle) => (WinGetProcessName(winTitle) == 'ApplicationFrameHost.exe')

  winTitle := "A"
  if fn(winTitle) {
    for hCtrl in WinGetControlsHwnd(winTitle)
      bool := fn(hCtrl)
    until !bool && winTitle := hCtrl
  }

  return WinGetProcessName(winTitle)
}

/**
 * ä»ç¯å¢ƒä¸­è¡¥å…¨ç¨‹åºçš„ç»å¯¹è·¯å¾„
 * æ¥æº: https://autohotkey.com/board/topic/20807-fileexist-in-path-environment/
 * @param target ç¨‹åºè·¯å¾„ 
 * @returns {string|any} 
 */
CompleteProgramPath(target) {

  ; å·¥ä½œç›®å½•ä¸‹çš„ç¨‹åº
  PathName := A_WorkingDir "\" target
  if FileExist(PathName)
    return PathName

  ; æœ¬èº«ä¾¿æ˜¯ç»å¯¹è·¯å¾„
  if FileExist(target)
    return target

  ; ä»ç¯å¢ƒå˜é‡ PATH ä¸­è·å–
  DosPath := EnvGet("PATH")
  loop parse DosPath, "`;" {
    if (A_LoopField)
      continue

    if FileExist(A_LoopField "\" target)
      return A_LoopField "\" target
  }

  ; ä»å®‰è£…çš„ç¨‹åºä¸­è·å–
  try {
    PathName := RegRead("HKLM", "SOFTWARE\Microsoft\Windows\CurrentVersion\App Paths\" target)
    if FileExist(PathName)
      return PathName
  }
}

/**
 * é€šè¿‡å‘½ä»¤è¡Œå»å¯åŠ¨ç¨‹åºï¼Œé˜²æ­¢ä¼šå¯¼è‡´ä»¥ç®¡ç†å‘˜å¯åŠ¨è½¯ä»¶çš„é—®é¢˜
 * @param target ç¨‹åºè·¯å¾„ 
 * @param arguments å‚æ•°
 * @param directory å·¥ä½œç›®å½•
 * @param operation é€‰é¡¹
 * @param show æ˜¯å¦æ˜¾ç¤º
 */
ShellRun(target, arguments?, directory?, operation?, show?) {
  static VT_UI4 := 0x13, SWC_DESKTOP := ComValue(VT_UI4, 0x8)
  ComObject("Shell.Application").Windows.Item(SWC_DESKTOP).Document.Application
    .ShellExecute(target, arguments?, directory?, operation?, show?)
}

/**
 * ä»¥ç®¡ç†å‘˜æƒé™æ‰“å¼€è½¯ä»¶
 * @param target ç¨‹åºè·¯å¾„
 * @param args å‚æ•°
 * @param workingDir å·¥ä½œç›®å½•
 */
RunAsAdmin(target, args, workingDir) {
  try {
    Run("*RunAs " target " " args, workingDir)
  } catch Error as e {
    Tip("ä½¿ç”¨ç®¡ç†å¯åŠ¨å¤±è´¥ " target ", " e.Message)
  }
}

/**
 * è¿è¡Œç¨‹åºæˆ–æ‰“å¼€ç›®å½•ï¼Œç”¨äºè§£å†³æ‰“å¼€çš„ç¨‹åºæ— æ³•è·å–ç„¦ç‚¹çš„é—®é¢˜
 * @param target ç¨‹åºè·¯å¾„
 * @param {string} args å‚æ•°
 * @param {string} workingDir å·¥ä½œç›®å½•
 * @param {number} admin æ˜¯å¦ä¸ºç®¡ç†å‘˜å¯åŠ¨
 * @returns {void} 
 */
RunPrograms(target, args := "", workingDir := "", admin := false) {
  ; è®°å½•å½“å‰çª—å£çš„hwndï¼Œå½“è½¯ä»¶å¯åŠ¨å¤±è´¥æ—¶è¿˜åŸç„¦ç‚¹
  currentHwnd := WinExist("A")
  ; é€šè¿‡ä¸€ä¸ªç•Œé¢å…ˆè·å–ç„¦ç‚¹å†æ‰§è¡Œå¯åŠ¨ç¨‹åºï¼Œå½“å¤±å»ç„¦ç‚¹æ—¶è‡ªå·±å…³é—­
  TempFocusGui().ShowGui()

  try {
    ; è¡¥å…¨ç¨‹åºè·¯å¾„
    programPath := CompleteProgramPath(target)
    if not (programPath) {
      ; æ²¡æœ‰æ‰¾åˆ°ç¨‹åºï¼Œå¯èƒ½æ˜¯ms-setting: ä¹‹ç±»çš„è¿æ¥
      Run(target " " args, workingDir)
      return
    }

    ; å¦‚æœæ˜¯æ–‡ä»¶å¤¹ç›´æ¥æ‰“å¼€
    if (InStr(FileExist(programPath), "D")) {
      Run(programPath)
      return
    }

    if (admin) {
      runAsAdmin(programPath, args, workingDir)
    } else {
      ShellRun(programPath, args, workingDir)
    }

  } catch Error as e {
    Tip(e.Message)
    ; è¿˜åŸçª—å£ç„¦ç‚¹
    WinActivate(currentHwnd)
    return
  }
}

/**
 * æ¿€æ´»çª—å£
 * @param winTitle AHKä¸­çš„WinTitle
 * @param {number} isHide çª—å£æ˜¯å¦ä¸ºéšè—çª—å£
 * @returns {number} 
 */
ActivateWindow(winTitle := "", isHide := false) {
  ; å¦‚æœåŒ¹é…ä¸åˆ°çª—å£ä¸”è®¤ä¸ºçª—å£ä¸ºéšè—çª—å£æ—¶æŸ¥æ‰¾éšè—çª—å£
  hwnds := FindWindows(winTitle, (hwnd) => WinGetTitle(hwnd) != "")
  if ((!hwnds.Length) && isHide) {
    hwnd := FindHiddenWindows(winTitle)
  }

  ; å¦‚æœåŒ¹é…åˆ°åˆ™è·³è½¬ï¼ŒåŒ¹é…ä¸åˆ°è¿”å›0
  if (!hwnds.Length) {
    return 0
  }

  ; åªæœ‰ä¸€ä¸ªçª—å£ä¸ºæœ€å°åŒ–åˆ™åˆ‡æ¢å¦åˆ™æœ€å°åŒ–
  if (hwnds.Length = 1) {
    hwnd := hwnds.Get(1)
    ; æŒ‡å®šä¸ä¸ºæ´»åŠ¨çª—å£æˆ–çª—å£è¢«ç¼©å°åˆ™æ˜¾ç¤ºå‡ºæ¥
    if (WinExist("A") != hwnd || WinGetMinMax(hwnd) = -1) {
      WinActivate(hwnd)
    } else {
      WinMinimize(hwnd)
    }
  } else {
    ; å¦‚æœå¤šä¸ªçª—å£åˆ™æ¥å›åˆ‡æ¢
    SwitchWindows(winTitle, hwnds)
  }

  return 1
}

/**
 * æŸ¥æ‰¾éšè—çª—å£è¿”å›çª—å£çš„Hwnd 
 * @param winTitle AHKä¸­çš„WinTitle
 * @returns {array} 
 */
FindHiddenWindows(winTitle) {
  WS_MINIMIZEBOX := 0x00020000
  WS_MINIMIZE := 0x20000000

  ; çª—å£è¿‡æ»¤æ¡ä»¶
  ; æ ‡é¢˜ä¸ä¸ºç©ºã€çª—å£å¤§å°å¤§äº400åŒ…å«æœ€å°åŒ–æŒ‰é’®æˆ–è¢«æœ€å°åŒ–äº†
  Predicate(hwnd) {
    if (WinGetTitle(hwnd) = "")
      return false

    style := WinGetStyle(hwnd)
    if not (style & WS_MINIMIZEBOX)
      return false

    WinGetPos(&x, &y, &windth, &height, hwnd)
    return (height > 400 && windth > 400) || (style & WS_MINIMIZE)
  }

  ; å¼€å¯å¯ä»¥æŸ¥æ‰¾åˆ°éšè—çª—å£
  DetectHiddenWindows true
  hwnds := FindWindows(winTitle, Predicate)
  DetectHiddenWindows false

  return hwnds
}

/**
 * è¿”å›ä¸æŒ‡å®šæ¡ä»¶åŒ¹é…çš„æ‰€æœ‰çª—å£
 * @param winTitle AHKä¸­çš„WinTitle
 * @param predicate è¿‡æ»¤çª—å£æ–¹æ³•ï¼Œä¼ è¿‡Hwndï¼Œè¿”å›bool
 * @returns {array} 
 */
FindWindows(winTitle, predicate?) {
  temps := WinGetList(winTitle)
  hwnds := []

  for hwnd in temps {
    hwnd := temps.Get(A_Index)
    ; å½“æœ‰è°“è¯æ¡ä»¶ä¸”æ»¡è¶³æ—¶æ·»åŠ è¿™ä¸ªhwnd
    if (IsSet(predicate) && (predicate(hwnd))) {
      hwnds.Push(hwnd)
    }
  }
  return hwnds
}

/**
 *  å°†ç¨‹åºè·¯å¾„æˆ–å‚æ•°ä¸­çš„{selected_text} æ›¿æ¢ä¸ºé€‰ä¸­çš„æ–‡å­—
 * @param target ç¨‹åºè·¯å¾„çš„å¼•ç”¨
 * @param args å‚æ•°çš„å¼•ç”¨
 * @returns {void|number} 
 */
ReplaceSelectedText(&target, &args) {
  text := GetSelectedText()
  if not (text) {
    return
  }

  if InStr(args, "://") || InStr(target, "://") {
    text := URIEncode(text)
  }
  args := strReplace(args, "{selected_text}", text)
  target := strReplace(target, "{selected_text}", text)

  return 1
}

/**
 * è·å–é€‰ä¸­çš„æ–‡å­—
 * @returns {void|string} 
 */
GetSelectedText() {
  temp := A_Clipboard
  ; æ¸…ç©ºå‰ªè´´æ¿
  A_Clipboard := ""

  Send("^c")
  if not (ClipWait(0.5)) {
    Tip("æ²¡æœ‰è·å–åˆ°æ–‡æœ¬", -700)
    return
  }
  text := A_Clipboard

  A_Clipboard := temp
  return RTrim(text, "`r`n")
}

/**
 * url ç¼–ç 
 * æ¥æº: https://www.autohotkey.com/boards/viewtopic.php?t=112741
 * @param Uri éœ€è¦ç¼–ç çš„æ–‡æœ¬
 * @param {string} encoding ç¼–ç æ ¼å¼
 * @returns {string} 
 */
URIEncode(Uri, encoding := "UTF-8") {
  var := Buffer(StrPut(Uri, encoding), 0)
  StrPut(Uri, var, encoding)
  pos := 1
  While pos <= StrLen(Uri) {
    code := NumGet(var, pos - 1, "UChar")
    if (code >= 0x30 && code <= 0x39) || (code >= 0x41 && code <= 0x5A) || (code >= 0x61 && code <= 0x7A)
      res .= Chr(code)
    else
      res .= "%" . Format("{:02X}", code)
    pos++
  }
  return res
}

/**
 * å¯åŠ¨InputHookï¼Œå¹¶è¿”å›EndReason
 * @param ih InputHookå¯¹è±¡
 * @returns {void} 
 */
StartInputHook(ih) {
  ; ç¦ç”¨æ‰€æœ‰çƒ­é”®
  Suspend(true)

  ; RAlt æ˜ å°„åˆ° LCtrl å,  æŒ‰ä¸‹ RAlt å†è§¦å‘ Capslock å‘½ä»¤ä¼šå¯¼è‡´ LCtrl é”®ä¸€ç›´å¤„äºæŒ‰ä¸‹çŠ¶æ€
  if GetKeyState("LCtrl") {
    Send("{LCtrl Up}")
  }

  ; å¯åŠ¨ç›‘å¬ç­‰å¾…è¾“å…¥åŒ¹é…åå…³é—­ç›‘å¬
  ih.Start()
  endReason := ih.Wait()
  ih.Stop()
  ; æ¢å¤æ‰€æœ‰çƒ­é”®
  Suspend(false)

  return endReason
}

/**
 * å‘é€æ¶ˆæ¯åˆ°å‘½ä»¤æç¤ºæ¡†
 * @param msg æ¶ˆæ¯ç¼–å·
 * @param {number} wParam æ¶ˆæ¯å‚æ•°
 */
PostMessageToCpasAbbr(msg, wParam := 0) {
  temp := A_DetectHiddenWindows
  DetectHiddenWindows(1)
  PostMessage(msg, wParam, 0, , "ahk_pid " capsAbbrWindowPid)
  DetectHiddenWindows(temp)
}

/**
 * å…³é—­é¡¶éƒ¨å‘½ä»¤æç¤ºæ¡†
 */
HideCaspAbbr() {
  HIDE_COMMAND_INPUT := 0x0400 + 0x0002
  PostMessageToCpasAbbr(HIDE_COMMAND_INPUT)
}

/**
 *  å°†é”®å…¥çš„å€¼å‘é€åˆ°è¾“å…¥æ¡†
 * @param ih InputHook å¯¹è±¡
 * @param char å‘é€çš„å­—ç¬¦
 */
PostCharToCaspAbbr(ih?, char?) {
  static SEND_CHAR := 0x0102
  PostMessageToCpasAbbr(SEND_CHAR, Ord(char))
}

/**
 * åˆ¤æ–­å½“å‰çª—å£æ˜¯ä¸æ˜¯æ¡Œé¢
 */
IsDesktop() {
  return WinActive("Program Manager ahk_class Progman") || WinActive("ahk_class WorkerW")
}

/**
 * è·å–å½“å‰ç„¦ç‚¹åœ¨å“ªä¸ªæ˜¾ç¤ºå™¨ä¸Š
 * @param x çª—å£Xè½´çš„é•¿åº¦
 * @param y çª—å£yè½´çš„é•¿åº¦
 * @param {number} default æ˜¾ç¤ºå™¨ä¸‹æ ‡
 * @returns {string|number} åŒ¹é…çš„æ˜¾ç¤ºå™¨ä¸‹æ ‡
 */
GetMonitorAt(x, y, default := 1) {
  m := SysGet(80)
  loop m {
    MonitorGet(A_Index, &l, &t, &r, &b)
    if (x >= l && x <= r && y >= t && y <= b)
      return A_Index
  }
  return default
}

/**
 * å½“å‰çª—å£æ˜¯æœ€å¤§åŒ–è¿˜æ˜¯æœ€å°åŒ–
 * @param {string} winTitle AHKä¸­çš„WinTitle
 * @returns {number} 
 */
WindowMaxOrMin(winTitle := "A") {
  return WinGetMinMax(winTitle)
}

/**
 * è·å–å…‰æ ‡çš„ä½ç½®
 * æ¥æºï¼šhttps://github.com/Ixiko/AHK-libs-and-classes-collection/blob/e5e1666d016c219dc46e7fc97f2bcbf40a9c0da5/AHK_V2/Misc.ahk#L328 GetCaretPos æ–¹æ³•
 * @param X å…‰æ ‡ç›¸å¯¹äºå±å¹•Xè½´çš„ä½ç½®
 * @param Y å…‰æ ‡ç›¸å¯¹äºå±å¹•Yè½´çš„ä½ç½®
 * @param W å…‰æ ‡çš„å®½åº¦
 * @param H å…‰æ ‡çš„é«˜åº¦
 * @returns {void} 
 */
GetCaretPos(&X?, &Y?, &W?, &H?) {
  ; UIA2 caret
  static IUIA := ComObject("{e22ad333-b25f-460c-83d0-0581107395c9}", "{34723aff-0c9d-49d0-9896-7ab52df8cd8a}")
  try {
    ComCall(8, IUIA, "ptr*", &FocusedEl := 0) ; GetFocusedElement
    ComCall(16, FocusedEl, "int", 10024, "ptr*", &patternObject := 0), ObjRelease(FocusedEl) ; GetCurrentPattern. TextPatternElement2 = 10024
    if patternObject {
      ComCall(10, patternObject, "int*", &IsActive := 1, "ptr*", &caretRange := 0), ObjRelease(patternObject) ; GetCaretRange
      ComCall(10, caretRange, "ptr*", &boundingRects := 0), ObjRelease(caretRange) ; GetBoundingRectangles
      if (Rect := ComValue(0x2005, boundingRects)).MaxIndex() = 3 { ; VT_ARRAY | VT_R8
        X := Round(Rect[0]), Y := Round(Rect[1]), W := Round(Rect[2]), H := Round(Rect[3])
        return
      }
    }
  }

  ; Acc caret
  static _ := DllCall("LoadLibrary", "Str", "oleacc", "Ptr")
  try {
    idObject := 0xFFFFFFF8 ; OBJID_CARET
    if DllCall("oleacc\AccessibleObjectFromWindow", "ptr", WinExist("A"), "uint", idObject &= 0xFFFFFFFF
      , "ptr", -16 + NumPut("int64", idObject == 0xFFFFFFF0 ? 0x46000000000000C0 : 0x719B3800AA000C81, NumPut("int64", idObject == 0xFFFFFFF0 ? 0x0000000000020400 : 0x11CF3C3D618736E0, IID := Buffer(16)))
      , "ptr*", oAcc := ComValue(9, 0)) = 0 {
      x := Buffer(4), y := Buffer(4), w := Buffer(4), h := Buffer(4)
      oAcc.accLocation(ComValue(0x4003, x.ptr, 1), ComValue(0x4003, y.ptr, 1), ComValue(0x4003, w.ptr, 1), ComValue(0x4003, h.ptr, 1), 0)
      X := NumGet(x, 0, "int"), Y := NumGet(y, 0, "int"), W := NumGet(w, 0, "int"), H := NumGet(h, 0, "int")
      if (X | Y) != 0
        return
    }
  }

  ; Default caret
  savedCaret := A_CoordModeCaret, W := 4, H := 20
  CoordMode "Caret", "Screen"
  CaretGetPos(&X, &Y)
  CoordMode "Caret", savedCaret
}

/**
 * å°†æ–‡æœ¬è½¬æ¢ä¸ºHtml
 * @param text éœ€è¦è½¬æ¢çš„æ–‡æœ¬
 * @param color HEXé¢œè‰²å€¼
 * @param fontFamily å­—ä½“
 * @returns {string} 
 */
FormatHtmlStyle(text, color, fontFamily) {
  style := "Color: '" color "'; font-fontFamily: '" fontFamily ";"

  text := HtmlEncode(text)
  html := "<HTML> <head><meta http-equiv='Content-type' content='text/html;charset=UTF-8'></head> <body> <!--StartFragment-->"
  if (InStr(text, "`n")) {
    html .= "<span style='" style "'><pre>" text "</pre></span>"
  } else {
    html .= "<span style='" style "'>" text "</span>"
  }
  html .= "<!--EndFragment--></body></HTML>"
  return html
}

/**
 * Htmlç¼–ç 
 * @param text éœ€è¦ç¼–ç çš„æ–‡æœ¬
 * @returns {void} 
 */
HtmlEncode(text) {
  text := strReplace(text, "&", "&amp;")
  text := strReplace(text, "<", "&lt;")
  text := strReplace(text, ">", "&gt;")
  text := strReplace(text, "" "", "&quot;")
  text := strReplace(text, " ", "&nbsp;")
  return text
}
