#Include TypoTipWindow.ahk

; æ‰˜ç›˜èœå•è¢«ç‚¹å‡»
TrayMenuHandler(ItemName, ItemPos, MyMenu) {
  switch ItemName {
    case "é€€å‡º":
      MyExit()
    case "æš‚åœ":
      ToggleSuspend()
    case "é‡å¯ç¨‹åº":
      ReloadPropram()
    case "æ‰“å¼€è®¾ç½®":
      OpenSettings()
    case "å¸®åŠ©æ–‡æ¡£":
      Run("https://xianyukang.com/MyKeymap.html")
    case "æŸ¥çœ‹çª—å£æ ‡è¯†ç¬¦":
      run("MyKeymap.exe bin\WindowSpy.ahk")

  }
}

; å…³é—­ç¨‹åº
MyExit() {
  thisPid := DllCall("GetCurrentProcessId")
  ProcessClose(thisPid)
}

; æš‚åœ
ToggleSuspend() {
  Suspend(!A_IsSuspended)
  if (A_IsSuspended) {
    TraySetIcon("./bin/icons/logo2.ico", , 1)
    A_TrayMenu.Check("æš‚åœ")
    Tip("  æš‚åœ MyKeymap  ", -500)
  } else {
    TraySetIcon("./bin/icons/logo.ico", , 0)
    A_TrayMenu.UnCheck("æš‚åœ")
    Tip("  æ¢å¤ MyKeymap  ", -500)
  }
}

; æ‰“å¼€è®¾ç½®
OpenSettings() {
  if (!WinExist("\bin\settings.exe"))
    Run("./bin/settings.exe ./bin")

  try {
    WinActivate("MyKeymap Settings")
  } catch Error as e {
    Run("http://127.0.0.1:12333")
  }
}

; é‡å¯ç¨‹åº
ReloadPropram() {
  Tip("Reload")
  Run("MyKeymap.exe")
}

; è‡ªåŠ¨å…³é—­çš„æç¤ºçª—å£
Tip(message, time := -1500) {
  ToolTip(message)
  SetTimer(() => ToolTip(), time)
}

; è·å–é¼ æ ‡ç§»åŠ¨æ—¶çš„æç¤ºçª—å£
GetMouseMovePromptWindow() {
  return TypoTipWindow("ğŸ–±", 16, 4, 0)
}

; ç§»åŠ¨é¼ æ ‡
MoveMouse(key, directionX, directionY, moveSingle, moveRepeat, showTip := false) {
  oneX := directionX * moveSingle
  oneY := directionY * moveSingle
  repeatX := directionX * moveRepeat
  repeatY := directionY * moveRepeat
  MouseMove(oneX, oneY, 0, "R")

  if showTip
    showTip.Show(, 19, 17)

  f() {
    if showTip
      showTip.Show(, 19, 17)
    MouseMove(repeatX, repeatY, 0, "R")
  }

  WhileKeyWait(key, moveDelay1, moveDelay2, f)
}

; å½“æŒ‰é”®ç­‰å¾…æ—¶æ‰§è¡Œçš„æ“ä½œ
WhileKeyWait(key, delay1, delay2, func) {
  i := KeyWait(key, delay1)
  while (!i) {
    func()

    i := KeyWait(key, delay2)
  }
}

; é€€å‡ºé¼ æ ‡ç§»åŠ¨æ¨¡å¼
ExitMouseMode() {
  global mouseMode := false

  Send("{Blind}{LButton up}")

  if (IsSet(mousemovePrompt))
    mousemovePrompt.show
}

; é¼ æ ‡ç‚¹å‡»åæ¨å‡º
MouseClickAndExit(key) {
  Send("{blind}" key)
  if (needExitMouseMode)
    ExitMouseMode()
}

; å†»ç»“éæŒ‡å®šçš„æ¨¡å¼
FreezeOtherMode(mode) {
  global activatedModes, customHotKey, altTabIsOpen, modeState
  customHotKey := true
  altTabIsOpen := false

  ; æ¯”å¦‚é”å®šäº† 3, ä½†åŒæ—¶æƒ³ç”¨ 9 æ¨¡å¼çš„çƒ­é”®, éœ€è¦ä¸´æ—¶å–æ¶ˆé”å®š
  if (modeState.locked) {
    %modeState.currentRef% := false
  }

  for index, value in activatedModes {
    if (value != mode) {
      Hotkey(value, "Off")
    }
  }
}

; é‡ç½®å½“å‰è¿è¡Œçš„çƒ­é”®
ResetCurrentMode(modeName, &modeRef) {
  global modeState
  if (modeState.locked)
    return

  modeState.currentName := modeName
  ; æŒ‡å®šå…¶å¼•ç”¨ï¼Œä¸ç„¶åé¢æ— æ³•æ”¹æ¨¡å¼çš„çŠ¶æ€
  modeState.currentRef := &modeRef
}

; ä¼˜å…ˆè§£å†»è¢«é”å®šæ¨¡å¼ï¼Œå¦‚æœæ²¡æœ‰è¢«é”å®šæ¨¡å¼åˆ™è§£å†»å…¨éƒ¨
UnfreezeMode(mode) {
  global activatedModes, customHotKey, altTabIsOpen, modeState
  customHotKey := false

  if (altTabIsOpen) {
    altTabIsOpen := false
    Send("{Enter}")
  }

  ; å¯åŠ¨è¢«é”å®šçš„æ¨¡å¼
  if (modeState.locked) {
    %modeState.currentRef% := true
  }

  for index, value in activatedModes {
    if (value != mode)
      Hotkey(value, "On")
  }

}

; å¯åŠ¨æŒ‡å®šMode
EnableMode(&mode, modeName, mil?, func?, needFreezeOtherMode := true) {
  statrtTick := A_TickCount
  thisHotKey := A_ThisHotkey
  mode := true
  ; Caps Fã€Caps ç©ºæ ¼ä¹‹ç±»çš„äºŒçº§æ¨¡å¼æ˜¯ä¸ç”¨è§¦å‘å†»ç»“çš„
  if (needFreezeOtherMode) {
    FreezeOtherMode(ThisHotkey)
    ResetCurrentMode(modeName, &mode)
  }
  KeyWait(thisHotKey)
  mode := false

  if (IsSet(mil))
    if ((A_PriorKey != "" && A_PriorKey = thisHotkey) && A_TickCount - statrtTick < mil)
      if (IsSet(func))
        func()

  ; å› ä¸ºæ²¡æœ‰è§¦å‘å†»ç»“æ‰€ä»¥ä¸éœ€è¦è§£å†»
  if (needFreezeOtherMode) {
    UnfreezeMode(ThisHotkey)
  }
}