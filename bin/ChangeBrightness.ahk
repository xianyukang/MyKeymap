#SingleInstance Force
#NoTrayIcon
TraySetIcon("./icons/logo.ico")

#Include ./lib/Monitor.ahk

class CLayout extends Gui {
  monitors := []
  currentIndex := 1
  monCount := SysGet(80)
  monitorCol := Monitor()
  defaultGuiWidth := 440
  monitorIcoSize := 170

  __New() {
    super.__New()
    super.Opt("+AlwaysOnTop +Owner +LastFound +Resize -MinimizeBox -MaximizeBox")
    super.Title := "æ˜¾ç¤ºå™¨äº®åº¦è°ƒèŠ‚"
    this.InitMonitors()
    this.ChangeSelectedMonitor()

    ; æ·»åŠ ä¸‹æ–¹å¸®åŠ©ä¿¡æ¯
    super.SetFont("s12")
    super.Add("text", "x10 y190 w290 h20", "EDSFè°ƒèŠ‚äº®åº¦ã€WRåˆ‡æ¢æ˜¾ç¤ºå™¨ã€Cé€€å‡º")
    super.Add("text", "x10 y210 w" this.defaultGuiWidth " h20", "å¦‚æœä¸èµ·ä½œç”¨ï¼Œå¯èƒ½æ˜¯å› ä¸ºä½¿ç”¨çš„æ˜¾ç¤ºå™¨æ²¡æœ‰å¼€å¯DDC/CIåŠŸèƒ½")
  }

  ; åˆå§‹åŒ–å·²ä½¿ç”¨çš„æ˜¾ç¤ºå™¨ä¿¡æ¯
  InitMonitors() {
    ; è·å–å·²ä½¿ç”¨çš„æ˜¾ç¤ºå™¨æ•°é‡
    loop this.monCount {
      monIndex := A_Index
      ; è·å–æ˜¾ç¤ºå™¨å½“å‰äº®åº¦
      brightness := this.getBrightness(monIndex)

      ; è®¡ç®—MonIcoTextè¦æ˜¾ç¤ºçš„ä½ç½®
      ; å½“æ˜¾ç¤ºå™¨å›¾æ ‡æ— æ³•æ²¾æ»¡çª—å£é»˜è®¤å¤§å°æ—¶,è®©æ˜¾ç¤ºå™¨å›¾æ ‡å‰§ä¸­
      padding := 0
      if(this.monitorIcoSize * this.monCount < this.defaultGuiWidth)  {
        padding := (this.defaultGuiWidth - (this.monitorIcoSize * this.monCount)) / 2
      }
      x := this.monitorIcoSize * (monIndex-1) + padding
      monIco := super.Add("Text", "x" x " y0 w" this.monitorIcoSize " h" this.monitorIcoSize " 0x1", "ğŸ–¥ï¸")
      monIco.SetFont("s128 c000000")

      ; è®¡ç®—æ˜¾ç¤ºå™¨äº®åº¦è¦æ˜¾ç¤ºçš„ä½ç½®
      x := x + 52
      monBrightness := super.Add("Text", "x" x " y52 w65 h40 0x1", brightness)
      monBrightness.SetFont("s32 cffffff")
      monBrightness.Opt("+BackgroundTrans")

      ; å°†å½“å‰æ§ä»¶ä¿å­˜èµ·æ¥
      m := Map()
      m["ico"] := monIco
      m["brightness"] := monBrightness
      this.monitors.Push(m)
    }
  }

  ; ä¿®æ”¹ç•Œé¢ä¸Šçš„å½“å‰æ˜¾ç¤ºå™¨
  ChangeSelectedMonitor(old := 1) {
    m := this.monitors.Get(old)
    m["ico"].SetFont("c000000")

    m := this.monitors.Get(this.currentIndex)
    m["ico"].SetFont("cff6688")
  }

  ; ä¸Šä¸€ä¸ªæ˜¾ç¤ºå±
  Previous() {
    if (this.currentIndex > 1) {
      this.changeSelectedMonitor(this.currentIndex--)
    }
  }
  
  ; ä¸‹ä¸€ä¸ªæ˜¾ç¤ºå±
  Next() {
    if (this.currentIndex < this.monCount) {
      this.changeSelectedMonitor(this.currentIndex++)
    }
  }

  ; åŠ äº®åº¦
  IncBrightness(mon) {
    m := this.monitors.Get(this.currentIndex)
    val := m["brightness"].Value+mon
    if (val <= 100) {
      this.SetBrightness(val, this.currentIndex)
      m["brightness"].Value := val
    }
  }

  ; å‡äº®åº¦
  DecBrightness(mon) {
    m := this.monitors.Get(this.currentIndex)
    val := m["brightness"].Value - mon
    if (val >= 0) {
      this.SetBrightness(val, this.currentIndex)
      m["brightness"].Value := val
    }
  }

  ; æ˜¾ç¤ºGUI
  ShowGui() {
    super.Show()
    this.disableIME(super.Hwnd)
  }

  ; è·å–å±å¹•äº®åº¦
  GetBrightness(monIndex) {
    try {
      brightness := this.monitorCol.GetBrightness(monIndex)["Current"]
    } catch Error as e {
      ; ä½¿ç”¨wmiè·å–äº®åº¦
      For property in ComObjGet( "winmgmts:\\.\root\WMI" ).ExecQuery( "SELECT * FROM WmiMonitorBrightness" )
        brightness := property.CurrentBrightness	
    }

    return brightness
  }

  ; è®¾ç½®å±å¹•äº®åº¦
  SetBrightness(brightness, monIndex, timeout := 1) {
    try {
      this.monitorCol.setBrightness(brightness, monIndex)
    } catch Error as e {
      ; ä½¿ç”¨wmiè®¾ç½®äº®åº¦
      For property in ComObjGet( "winmgmts:\\.\root\WMI" ).ExecQuery( "SELECT * FROM WmiMonitorBrightnessMethods" )
        property.WmiSetBrightness( timeout, brightness )
    }
  }

  DisableIME(hwnd) {
    controlName := ControlGetFocus(hwnd)
    controlHwnd := ControlGetHwnd(controlName)
    DllCall("Imm32\ImmAssociateContext", "ptr", controlHwnd, "ptr", 0, "ptr")
  }
}

layout := CLayout()
layout.ShowGui()

SetTimer(IfLoseFocusThenExit, 100)
IfLoseFocusThenExit() {
  if(not WinActive("ahk_id" layout.Hwnd)) {
    ExitApp
  }
}

OnMessage(0x100, WM_KEYDOWN)
WM_KEYDOWN(wParam, lParam, msg, hwnd) {
  switch(GetKeyName(Format("vk{:x}", wparam))) {
    case "w": layout.Previous()
    case "r": layout.Next()
    case "e": layout.IncBrightness(10)
    case "f": layout.IncBrightness(5)
    case "d": layout.DecBrightness(10)
    case "s": layout.DecBrightness(5)
    case "c":  ExitApp
    case "Escape": ExitApp
  }

  return 0
}
